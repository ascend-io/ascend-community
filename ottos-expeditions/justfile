# justfile

# load environment variables
set dotenv-load

# variables
locallakedir:="lake"
remotelakedir:="lakev0"

# aliases
alias fmt:=format
alias install:=sync

# list justfile recipes
default:
    just --list

# unmesh the mesh project into data plane projects
unmesh *args:
    cd oeutils && just run unmesh {{ args }}

# python things
setup:
    uv venv --python=3.12 --allow-existing
    just sync

sync:
    uv sync --all-extras --upgrade

build:
    rm -r dist || true
    uv build

format:
    ruff format .
    cd oeutils && just format

# cloud stuff
upload-az:
    azcopy sync {{locallakedir}} "https://ascendiosampledata.blob.core.windows.net/ottos-expeditions/{{remotelakedir}}"

upload-gs:
    gsutil -m rsync -r {{locallakedir}} gs://ascend-io-gcs-public/ottos-expeditions/{{remotelakedir}}

upload-az-seed:
    azcopy sync {{locallakedir}}/seed "https://ascendiosampledata.blob.core.windows.net/ottos-expeditions/{{remotelakedir}}/seed"

upload:
    @echo "uploading to az..."
    just upload-az
    @echo "\n\nuploading to gs..."
    just upload-gs

# clean
clean-gs:
    gsutil -m rm -r gs://ascend-io-gcs-public/ottos-expeditions/{{remotelakedir}}

clean-local:
    rm -rf {{locallakedir}}

# release-test-pypi
release-test-pypi:
    just build
    uv publish --publish-url https://test.pypi.org/legacy/ --token ${PYPI_TEST_TOKEN}

# release-pypi
release-pypi:
    just build
    uv publish --token ${PYPI_TOKEN}

# release-repo
release-repo: unmesh
    #!/usr/bin/env bash

    # check `just unmesh` resulted in no changes
    [ -z "$(git status --porcelain)" ] || exit 1

    # check if we are on the main branch
    if [ "$(git rev-parse --abbrev-ref HEAD)" != "main" ]; then
        echo "You must be on the main branch to release."
        exit 1
    fi

    # check if we are up to date with origin/main
    if [ "$(git rev-parse HEAD)" != "$(git rev-parse origin/main)" ]; then
        echo "You must be up to date with origin/main to release."
        exit 1
    fi

    # change up
    cd ..

    # clone public repo
    gh repo clone ascend-io/ascend-community public-repo

    # change into it
    cd public-repo

    # remove ottos-expeditions
    rm -r ottos-expeditions

    # copy ottos-expeditions from internal repo
    cp -r ../ottos-expeditions .

    # delete internal stuff
    rm -r ottos-expeditions/projects/mesh
    rm -r ottos-expeditions/oeutils
    rm -r ottos-expeditions/codex.md

    # commit and push
    git add .
    git commit -m "sync from internal repo"
    git push

    # cleanup
    cd ..
    rm -rf public-repo
    cd ottos-expeditions
